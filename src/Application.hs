{-# LANGUAGE DataKinds #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE RecordWildCards #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE ViewPatterns #-}
{-# OPTIONS_GHC -fno-warn-orphans #-}

module Application (
    makeApp,
    runApp,
    runMigrations,
) where

import qualified Database as DB
import Foundation
import Handler
import qualified Logger
import Settings

import Control.Monad (when)
import qualified Network.Wai.Handler.Warp as Warp
import Say (say)
import Yesod.Core

-- Generate dispatch code linking requests for routes to handler functions.
mkYesodDispatch "App" resourcesApp

-- | Create and run the gsd-server application.
runApp :: FilePath -> IO ()
runApp filePath = do
    settings <- loadSettings filePath
    app <- makeApp settings
    waiApp <- makeWaiApplication app
    say $ "Running kb-server on port " <> settingsHttpPort settings
    Warp.runSettings (warpSettings app) waiApp

-- | Create the core application
makeApp :: Settings -> IO App
makeApp appSettings = do
    appConnectionPool <- DB.createPool appSettings
    appLogger <- Logger.makeAppLogger
    when (settingsRunMigrations appSettings) $
        DB.runMigrations appSettings appConnectionPool
    pure App{..}

-- Create a WAI Application and apply request logger middleware.
makeWaiApplication :: App -> IO Application
makeWaiApplication app = do
    waiApp <- toWaiAppPlain app
    requestLoggerMiddleware <- Logger.makeRequestLogger app
    pure $ requestLoggerMiddleware waiApp

-- Create warp settings for App (!4 means HostIPv4Only).
warpSettings :: App -> Warp.Settings
warpSettings app =
    Warp.setPort (settingsReadHttpPort $ appSettings app) $
        Warp.setHost "!4" Warp.defaultSettings

-- | Run database migrations without starting the application.
runMigrations :: FilePath -> IO ()
runMigrations filePath = do
    settings <- loadSettings filePath
    pool <- DB.createPool settings
    say $ "Running migrations in the " <> settingsEnvironment settings <> " environment"
    DB.runMigrations settings pool
